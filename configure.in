dnl Process this file with autoconf to produce a configure script.
dnl Copyright (c) Daniel Grimwood, October 2000.

dnl ****************************************************************************
dnl Define macros first
dnl ****************************************************************************

AC_DEFUN(DAN_GET_COMPILER,
[
if test ${compiler} = UNKNOWN; then
  ${PERL} -e 'open(A,"conf.test"); while (<A>) {if (/$1/) {exit 0}; exit 1;}'
  if test ${?} = 0; then
    compiler=$2
  fi
fi
])

AC_DEFUN(DYLAN_CHECK_IF_F95_COMPILER,
[
cat > conftest.$1 <<EOF
    program main
      integer :: i
      real, dimension(10) :: a
      forall(i=1:10)
         a(i) = 1.0
      end forall
    end
EOF
rm -f conftest.x conftest.o
csh -c "$FC -O0 -o conftest.x conftest.$1 >& /dev/null"
if eval "test -x conftest.x"; then
   f95_compiler="yes"
else
   f95_compiler="no"
fi
dnl rm -f conftest.x conftest.$1
])

AC_DEFUN(DAN_CHECK_SUFFIX,
[
if test ${havesuffix} != "1"; then
cat > conftest.$1 <<EOF
      program main
        write(*,*) 1
      end
EOF
rm -f conftest.x conftest.o
csh -c "$FC -O0 -o conftest.x conftest.$1 >& /dev/null"
if eval "test -x conftest.x"; then
  havesuffix="1"
  FSUFFIX=$1
fi
rm -f conftest.x conftest.$1 conftest.o
fi
])

AC_DEFUN(DAN_CHECK_LAPACK,
[
if test ${havelibs} != "1"; then
ac_save_LIBS="$LIBS"
AC_MSG_CHECKING("for $1")
LIBS="$1 $LIBS"
cat > conftest.${FSUFFIX} <<EOF
      program main
        write(*,*) lsame("a","b")
        call dgesv(4,nrhs,LU,4,pivot,solution,4,err)
      end
EOF
rm -f conftest.x conftest.o
csh -c "$FC -O0 $LIBS -o conftest.x conftest.${FSUFFIX} >& /dev/null"
LIBS="$ac_save_LIBS"
if eval "test -x conftest.x"; then
  AC_MSG_RESULT(yes)
  havelibs="1"
  LIBS="$1 $LIBS"
else
  AC_MSG_RESULT(no)
fi
rm -f conftest.x conftest.${FSUFFIX} conftest.o
fi
])

AC_DEFUN(DAN_CHECK_ESSL,
[
if test ${havelibs} != "1"; then
ac_save_LIBS="$LIBS"
AC_MSG_CHECKING("for $1")
LIBS="$1 $LIBS"
cat > conftest.${FSUFFIX} <<EOF
      program main
        write(*,*) lsame("a","b")
        call dges(LU,4,4,pivot,solution,0)
      end
EOF
rm -f conftest.x conftest.o
csh -c "$FC -O0 $LIBS -o conftest.x conftest.${FSUFFIX} >& /dev/null"
LIBS="$ac_save_LIBS"
if eval "test -x conftest.x"; then
  AC_MSG_RESULT(yes)
  havelibs="1"
  essl="1"
  LIBS="$1 $LIBS"
else
  AC_MSG_RESULT(no)
fi
rm -f conftest.x conftest.${FSUFFIX} conftest.o
fi
])

AC_DEFUN(DAN_CHECK_PROG_WARN,
[
AC_CHECK_PROG($1,$2,$2,)
test -n "[$]$1" || AC_MSG_WARN("$3")
])

AC_DEFUN(DAN_CHECK_PROG_ERROR,
[
AC_CHECK_PROG($1,$2,$2,)
test -n "[$]$1" || AC_MSG_ERROR("$3")
])

AC_DEFUN(DAN_CHECK_PROGS_WARN,
[
for dan_prog in $2
do
AC_CHECK_PROG($1, [$]dan_prog, [$]dan_prog,,)
test -n "[$]$1" && break
done
ifelse([$3], , , [test -n "[$]$1" || $1=""
])
test -n "[$]$1" || AC_MSG_WARN("$3")
])

AC_DEFUN(DAN_CHECK_F90,
[
$3="N"
for dan_prog in $2
do
AC_CHECK_PROG($1, [$]dan_prog, [$]dan_prog,,)
test -n "[$]$1" && break
done
ifelse([$3], , , [test -n "[$]$1" || $1=""
])
test -n "[$]$1" || $3="Y"
])

AC_DEFUN(DAN_CHECK_GNU_MAKE,
[
AC_MSG_CHECKING("whether GNU make")
${MAKE} -v -f/dev/null > conf.test 2>&1
${PERL} -e 'open(A,"conf.test"); while (<A>) {if (/GNU/) {exit 0}; exit 1;}'
if test ${?} = 1; then
  rm -f conf.test
  AC_MSG_RESULT("no")
  AC_MSG_ERROR("GNU make required")
else
  rm -f conf.test
  AC_MSG_RESULT("yes")
fi
])

dnl ****************************************************************************
dnl                   The actual script follows now
dnl ****************************************************************************

define([AC_CACHE_LOAD], )dnl
define([AC_CACHE_SAVE], )dnl
AC_INIT(Makefile.in)

dnl Checks for programs.

AC_MSG_RESULT("")
AC_CHECKING(Programs)
DAN_CHECK_PROG_ERROR(PERL,perl,"perl not found - cannot continue")
DAN_CHECK_PROG_ERROR(UNAME,uname,"uname not found - cannot continue")
DAN_CHECK_PROG_WARN(CMP,cmp,"cmp not found - cannot build executables")
DAN_CHECK_PROGS_WARN(MAKE,gmake make,"make not found - cannot continue")
if test -n ${MAKE}; then
DAN_CHECK_GNU_MAKE
fi

dnl Get OS Type

AC_MSG_RESULT("")
AC_MSG_CHECKING(Operating system)
uname=`${UNAME}`
OS=UNKNOWN
test ${uname} = Linux  && OS=LINUX
test ${uname} = OSF1   && OS=OSF1
test ${uname} = AIX    && OS=AIX
test ${uname} = HP-UX  && OS=HPUX
test ${uname} = SunOS  && OS=SUNOS
test ${uname} = IRIX64 && OS=IRIX64
AC_MSG_RESULT(${OS})

dnl Get Fortran Compiler name.

AC_MSG_RESULT("")
AC_CHECKING(Fortran compiler)
DAN_CHECK_F90(FC,f95 f90 frtvpp frt xlf90 g95 g90,DEFAULT)
AC_MSG_CHECKING(Fortran vendor)
compiler=UNKNOWN
if test ${DEFAULT} = "Y"; then
  AC_MSG_RESULT("UNKNOWN")
  FC=f95
  FSUFFIX=f90
  AC_MSG_WARN("No fortran compiler found!")
  AC_MSG_WARN("Skipping library checks.")
else
  ${FC} -V > conf.test 2>&1 || ${FC} -v > conf.test 2>&1 || ${FC} -version > conf.test 2>&1
  DAN_GET_COMPILER(Fujitsu,FUJITSU)
  DAN_GET_COMPILER(Compaq,COMPAQ)
  DAN_GET_COMPILER(Digital,DEC)
  DAN_GET_COMPILER(IBM,IBM)
  DAN_GET_COMPILER(NAG,NAG)
  DAN_GET_COMPILER(HP,HP)
  DAN_GET_COMPILER(WorkShop,WORKSHOP)
  DAN_GET_COMPILER(MIPSpro,MIPSPRO)
  rm -f conf.test 
  rm -f a.out
  AC_MSG_RESULT(${compiler})

fi

SYSCOMP="${compiler}-${FC}-on-${OS}"


dnl insert compiler dependent flags into Makefile, from the site_config/$SYSCOMP file.

AC_MSG_RESULT("")

SITECONFIG="${srcdir}/site_config/${SYSCOMP}"
SITE_CONFIG=${SITECONFIG}
AC_MSG_RESULT("Your site_config file is ${SITECONFIG}")

if test ${compiler} = "UNKNOWN" -o ! -f ${SITECONFIG}; then
  test -f ${SITECONFIG} || cp -f ${srcdir}/site_config/template ${SITECONFIG}
  tmp="I do not know the settings for your compiler, using defaults."
  AC_MSG_RESULT(${tmp})
  tmp="You will need to edit this file, then rerun configure."
  AC_MSG_RESULT(${tmp})
  tmp="If the compiler vendor was recognised, try using another"
  AC_MSG_RESULT(${tmp})
  tmp="site_config file with this vendor as a guess."
  AC_MSG_RESULT(${tmp})
else
  tmp="You may want to edit this file, but it should work as is."
  AC_MSG_RESULT(${tmp})
fi
AC_MSG_RESULT("See the documentation for more information.")
AC_MSG_RESULT("")

AC_SUBST_FILE(SITECONFIG)
AC_SUBST(PERL)
AC_SUBST(MAKE)
AC_SUBST(prefix)
AC_SUBST(exec_prefix)
AC_SUBST(bindir)
AC_SUBST(SITE_CONFIG)
AC_OUTPUT(Makefile)
AC_MSG_RESULT("")
tmp="Next, type \"${MAKE} install\""
AC_MSG_RESULT(${tmp})
